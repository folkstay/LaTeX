--Режим явных транзакций (explicit transaction).
/*
	Явная транзакция выполняется, если для запуска транзакции явно указана команда BEGIN TRANSACTION (BEGIN TRAN).
	При старте явной транзакции @@TRANCOUNT увеличивается на 1. Тело транзакции может содержать команды DML и DDL,
	и завершается инструкцией COMMIT или ROLLBACK.
	Явные транзакции можно выполнять в интерактивном режиме или в коде(например, в хранимой процедуре).
	Явные транзакции могут использоваться в режиме неявных транзакций. Если запускается явная транзакция, для сеанса,
	работающего в режиме неявных транзакций, значение @@TRANCOUNT будет увеличено  с 0 до 2 сразу после команды 
	BEGIN TRANSACTION. В действительности получается вложенная транзакция.
	Если какие-либо изменения данных или инструкции DDL вызывают ошибку, то некоторые ошибки приводят к откату всей транзакции,
	а некоторые (например, неверный внешний ключ), не вызывают отката транзакции целиком. Для обеспечения корректного выполнения
	транзакции необходимо использовать обработку ошибок.
*/
--Начальные условия
/*
-- Удаление записей в таблице
 TRUNCATE TABLE [Счета]
 GO
 --Добавление строк в таблицу
 INSERT INTO  [Счета]([Номер счета], [Владелец], [Баланс])
 VALUES ('1111111111111',N'Иванов',1000),
		('2222222222222',N'Петров',0)
GO
*/
SET NOCOUNT ON 
--Проверка исходного состояния
SELECT * FROM [Счета]
--Неструктурированная обработка ошибок может быть основана на проверке значения системной функции @@ERROR
--после выполнения каждой инструкции DML. При возникновении ошибки в инструкции DDL значение @@ERROR
--будет содержать номер ошибки. При успешном завершении @@ERROR = 0. Обратите внимание, что @@ERROR всегда 
--выводит состояние ошибки последней выполненной команды, поэтому любой запрос функции @@ERROR, например, в предложении IF
--приводит к её сбросу и присвоению нового номера.
--Поэтому лучше сохранять значение @@ERROR  в переменной, и затем проверять эту переменную.

--Пример использования явной транзакции (выполнить 2 раза)
BEGIN TRANSACTION
DECLARE @ErrorVar1 int, @ErrorVar2 int

SELECT @@TRANCOUNT AS 'Количество транзакций';


UPDATE [Счета]
SET [Баланс]-=1000
WHERE [Владелец]=N'Иванов'

--Обработка ошибок с помощью @@ERROR
SET @ErrorVar1 = @@ERROR
IF @ErrorVar1<>0
	PRINT N'Ошибка снятия денег со счёта Иванова. Код ошибки : ' + CAST(@ErrorVar1 as NVARCHAR)
ELSE
	PRINT N'Деньги сняты со счёта Иванова'

SELECT N'После попытки снять 1000 у Иванова' AS [состояние],
	* FROM [Счета]

UPDATE [Счета]
SET [Баланс]+=1000
WHERE [Владелец]=N'Петров'

--Обработка ошибок с помощью @@ERROR
SET @ErrorVar2 = @@ERROR
IF @ErrorVar2<>0
	PRINT N'Ошибка зачисления денег на счёт Петрова. Код ошибки : ' + CAST(@ErrorVar2 as NVARCHAR)
ELSE
	PRINT N'Деньги зачислены на счёт Петрова'
SELECT N'После попытки зачислить деньги Петрову' AS [состояние],
	* FROM [Счета]
--Фиксируем или откатываем транзакцию в зависимости от наличия ошибок в инструкциях снятия и зачисления
 IF (@ErrorVar1=0) and  (@ErrorVar2=0)
	BEGIN
		PRINT N' Фиксация транзакции. Перевод денег успешно завершен.'
		COMMIT TRANSACTION
	END
ELSE
	BEGIN
		PRINT N' Откат транзакции. Перевод денег не выполнен.'
		ROLLBACK TRANSACTION
	END

SELECT N'После окончания транзакции' AS [состояние],
	* FROM [Счета]
GO

--Точки сохранения
BEGIN TRANSACTION

UPDATE [Счета]
SET [Баланс] = 
	CASE  [Владелец]
			WHEN N'Иванов' THEN [Баланс] - 700
			WHEN N'Петров' THEN [Баланс] + 700
	END
WHERE [Владелец] in (N'Иванов', N'Петров')
GO
SELECT N'После 1 перевода' AS [состояние],@@TRANCOUNT [TRANCOUNT],
	* FROM [Счета]

SAVE TRANSACTION point1


UPDATE [Счета]
SET [Баланс] = 
	CASE  [Владелец]
			WHEN N'Иванов' THEN [Баланс] + 200
			WHEN N'Петров' THEN [Баланс] - 200
	END
WHERE [Владелец] in (N'Иванов', N'Петров')
GO
-- откат всей транзакции
--IF @@ROWCOUNT<2
--	ROLLBACK
SELECT N'После 2 перевода' AS [состояние],@@TRANCOUNT [TRANCOUNT],
	* FROM [Счета]
--откат к точке сохранения
ROLLBACK TRANSACTION point1

SELECT N'После отката' AS [состояние],@@TRANCOUNT [TRANCOUNT],
	* FROM [Счета]

-- Фиксация или откат
COMMIT TRANSACTION
---ROLLBACK TRANSACTION
SELECT N'После завершения' AS [состояние],@@TRANCOUNT [TRANCOUNT],
	* FROM [Счета]