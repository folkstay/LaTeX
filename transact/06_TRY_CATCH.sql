-- Использование TRY CATCH
--Начальные условия
/*
-- Удаление записей в таблице
 TRUNCATE TABLE [Счета]
 GO
 --Добавление строк в таблицу
 INSERT INTO  [Счета]([Номер счета], [Владелец], [Баланс])
 VALUES ('1111111111111',N'Иванов',1000),
		('2222222222222',N'Петров',0)
GO
*/
SET NOCOUNT ON 
--Выполнить 2 раза
--Проверка исходного состояния
SELECT N'До начала транзакции' AS [Статус], @@TRANCOUNT AS [Количество транзакций], * FROM [Счета]

--Старт транзакции
BEGIN TRANSACTION
	BEGIN TRY
		UPDATE [Счета]
		SET [Баланс]-=1000
		WHERE [Владелец]=N'Иванов'
		
		SELECT N'После попытки снять 1000 у Иванова' AS [состояние], @@TRANCOUNT AS [Количество транзакций],* FROM [Счета]

		UPDATE [Счета]
		SET [Баланс]+=1000
		WHERE [Владелец]=N'Петров'

		SELECT N'После попытки зачислить деньги Петрову' AS [состояние], @@TRANCOUNT AS [Количество транзакций],* FROM [Счета]
		if @@TRANCOUNT>0
		BEGIN
			PRINT N' Фиксация транзакции. Перевод денег успешно завершен.'
			COMMIT TRANSACTION
		END
	END TRY
	BEGIN CATCH
		SELECT ERROR_NUMBER() AS [Номер ошибки],
			   ERROR_MESSAGE() AS [Сообщение об ошибке],
			   ERROR_SEVERITY() AS [Уровень серьезности ошибки],
			   ERROR_LINE() AS [Номер строки в пакете, где произошла ошибка],
			   ERROR_PROCEDURE() AS [Имя функции(процедуры или триггера), которые выполнялись в момент возникновения ошибки],
			   ERROR_STATE() AS [Сосотяние ошибки],
			   @@TRANCOUNT AS [Количество транзакций]
		if @@TRANCOUNT>0
		BEGIN
			PRINT N' Откат транзакции. Перевод денег не выполнен.'
			ROLLBACK TRANSACTION
		END; -- перед THROW обязательно (;)
		-- Инструкция THROW без параметров используется для повторной инциации сообщения об ошибке и
		-- отправки его обратно клиенту. Это один из способов передачи ошибки вызывающей стороне.
		-- После отправки ошибки клиенту выполнение пакета будет прервано.
		THROW;
	END CATCH
SELECT N'После окончания транзакции' AS [состояние], @@TRANCOUNT AS [Количество транзакций],* FROM [Счета]